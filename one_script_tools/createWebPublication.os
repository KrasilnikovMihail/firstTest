#Использовать v8runner
#Использовать cmdline
#Использовать 1commands

Перем СЕРВЕР;
Перем СЕРВЕР_ПОРТ;
Перем БАЗА;
Перем ЭТО_ФАЙЛОВАЯ_БАЗА;
Перем ПОЛЬЗОВАТЕЛЬ;
Перем ПАРОЛЬ;
Перем ПЛАТФОРМА_ВЕРСИЯ;
Перем ИМЯ_ПУБЛИКАЦИИ;
Перем КАТАЛОГ_ПУБЛИКАЦИИ;

Перем Конфигуратор;
Перем Лог;

Функция Инициализация()

    Парсер = Новый ПарсерАргументовКоманднойСтроки();
    Парсер.ДобавитьИменованныйПараметр("-platform");
    Парсер.ДобавитьИменованныйПараметр("-server");
    Парсер.ДобавитьИменованныйПараметр("-base");
    Парсер.ДобавитьИменованныйПараметр("-user");
    Парсер.ДобавитьИменованныйПараметр("-passw");
    Парсер.ДобавитьИменованныйПараметр("-publicname");
    Парсер.ДобавитьИменованныйПараметр("-publicpath");

    Параметры = Парсер.Разобрать(АргументыКоманднойСтроки);
    
    ПЛАТФОРМА_ВЕРСИЯ  = Параметры["-platform"];//"8.3.10.2639"; // если пустая строка, то будет взята последняя версия
    СЕРВЕР            =  Параметры["-server"];
    СЕРВЕР_ПОРТ       = 1541; // 1541 - по умолчанию
    БАЗА              = Параметры["-base"];
    ЭТО_ФАЙЛОВАЯ_БАЗА = Не ЗначениеЗаполнено(СЕРВЕР);
    ПОЛЬЗОВАТЕЛЬ      = Параметры["-user"];
    ПАРОЛЬ            = Параметры["-passw"];
    ИМЯ_ПУБЛИКАЦИИ    = Параметры["-publicname"];
    КАТАЛОГ_ПУБЛИКАЦИИ = Параметры["-publicpath"];

    //ПЛАТФОРМА_ВЕРСИЯ  = "8.3.12.1685";
    //СЕРВЕР            = "devadapter";
    //СЕРВЕР_ПОРТ       = 1541; // 1541 - по умолчанию
    //БАЗА              = "rkudakov_adapter_adapter";
    //ЭТО_ФАЙЛОВАЯ_БАЗА = Не ЗначениеЗаполнено(СЕРВЕР);
    //ПОЛЬЗОВАТЕЛЬ      = "Administrator";
    //ПАРОЛЬ            = "111";
    //ИМЯ_ПУБЛИКАЦИИ = "myweb";
    //КАТАЛОГ_ПУБЛИКАЦИИ = "c:\www\www3";

    Конфигуратор = Новый УправлениеКонфигуратором();
    Конфигуратор.УстановитьКонтекст(СтрокаСоединенияИБ(), ПОЛЬЗОВАТЕЛЬ, ПАРОЛЬ);
    Конфигуратор.ИспользоватьВерсиюПлатформы(ПЛАТФОРМА_ВЕРСИЯ);

    Лог = Логирование.ПолучитьЛог("createExtensionRepo");

    ЛОГ.Отладка("СЕРВЕР = " + СЕРВЕР);
    ЛОГ.Отладка("ПЛАТФОРМА_ВЕРСИЯ = " + ПЛАТФОРМА_ВЕРСИЯ);

    Файл = Новый Файл(КАТАЛОГ_ПУБЛИКАЦИИ);
    Если Не Файл.Существует() Тогда
        СоздатьКаталог(КАТАЛОГ_ПУБЛИКАЦИИ);
    КонецЕсли;

КонецФункции

Функция ОпубликоватьВебКлиент()

    ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();

    ПутьКУтилитеПубл = СтрЗаменить(Конфигуратор.ПутьКПлатформе1С(), "1cv8.exe", "webinst");

    Команда = Новый Команда;
    Команда.УстановитьКоманду(ПутьКУтилитеПубл);

    Параметры = Новый Массив;
    Параметры.Добавить("-publish");
    Параметры.Добавить("-iis"); // поддерживается только IIS веб-сервер
    Параметры.Добавить("-wsdir " + ИМЯ_ПУБЛИКАЦИИ);
    Параметры.Добавить(СтрШаблон("-dir ""%1""", КАТАЛОГ_ПУБЛИКАЦИИ));
    Параметры.Добавить(СтрШаблон("-connstr ""Srvr=%1:%2;Ref=%3;""", СЕРВЕР, СЕРВЕР_ПОРТ, БАЗА));
    
    Команда.ДобавитьПараметры(Параметры);
    Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
    Команда.ПоказыватьВыводНемедленно(Истина);
    КодВозврата = Команда.Исполнить();
    Если КодВозврата <> 0 Тогда
        ВызватьИсключение Команда.ПолучитьВывод();
    КонецЕсли;

    Вывод = Команда.ПолучитьВывод();

КонецФункции

Функция СтрокаСоединенияИБ() 
    Если ЭТО_ФАЙЛОВАЯ_БАЗА Тогда
        Возврат "/F" + БАЗА; 
    Иначе   
        Возврат "/IBConnectionString""Srvr=" + СЕРВЕР + ?(ЗначениеЗаполнено(СЕРВЕР_ПОРТ),":" + СЕРВЕР_ПОРТ,"") + ";Ref='"+ БАЗА + "'""";
    КонецЕсли;
КонецФункции

Инициализация();
Лог.Информация("Creating web publucation for base %1", БАЗА);
ОпубликоватьВебКлиент();
Лог.Информация("Sucessfuly created web publication for base %1", БАЗА);