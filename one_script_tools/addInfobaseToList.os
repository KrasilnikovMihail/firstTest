#Использовать v8runner
#Использовать cmdline

Перем БАЗА;
Перем СЕРВЕР;
Перем ПОЛЬЗОВАТЕЛЬ;

Перем Лог;

Функция Инициализация()

    Лог = Логирование.ПолучитьЛог("addInfobaseToList");
    Лог.УстановитьУровень(УровниЛога.Информация);

    Парсер = Новый ПарсерАргументовКоманднойСтроки();
    Парсер.ДобавитьИменованныйПараметр("-base");
    Парсер.ДобавитьИменованныйПараметр("-server");
    Парсер.ДобавитьИменованныйПараметр("-user");

    Параметры = Парсер.Разобрать(АргументыКоманднойСтроки);

    ПОЛЬЗОВАТЕЛЬ     = Параметры["-user"];
    БАЗА             = НРег(Параметры["-base"]);
    СЕРВЕР           = Параметры["-server"];

    //ПОЛЬЗОВАТЕЛЬ     = "rkudakov";
    //БАЗА             = "rkudakov_adapter_adapter";
    //СЕРВЕР           = "devadapter";)

КонецФункции

Функция ПолучитьПапкуПользователей() 

    КаталогиПользователей = Новый Массив;
    Shell = Новый COMОбъект("WScript.Shell");
    КаталогиПользователей.Добавить("C:\Users\");
    КаталогиПользователей.Добавить("D:\Users\");
    КаталогиПользователей.Добавить("E:\Users\");

    ПапкаИзСкрипта = (Shell.ExpandEnvironmentStrings("%homedrive%") + "\users\"); // Не работает на нодах дженкинса в сервисном режиме
    Если КаталогиПользователей.Найти(ПапкаИзСкрипта) = 0 Тогда
        КаталогиПользователей.Добавить(ПапкаИзСкрипта);
    КонецЕсли;

    Возврат КаталогиПользователей;

КонецФункции

Процедура ДобавитьБазуВСписок()

    Если Не ЗначениеЗаполнено(ПОЛЬЗОВАТЕЛЬ) Тогда
        Лог.Информация("User is empty so the step skiped...");
        Возврат;
    КонецЕсли;

    Shell = Новый COMОбъект("WScript.Shell");
    КаталогиЮзеров =  ПолучитьПапкуПользователей();
    Для каждого КаталогЮзера Из КаталогиЮзеров Цикл
        Лог.Информация(КаталогЮзера);
        Попытка
            Файл = Новый Файл(КаталогЮзера);
            Если Не Файл.Существует() Тогда
                Продолжить;
            КонецЕсли;
            
            // Обновляем список баз текущего пользователя
            userProfile = КаталогЮзера + ПОЛЬЗОВАТЕЛЬ;
            ПапкаAPPDATA = СтрШаблон("%1\AppData\Roaming", userProfile);
            ОбновитьБазуВСпискеДЛяПользователя(ПапкаAPPDATA + "\1C\1CEStart\ibases.v8i", userProfile);

            // Обновляем список баз пользователя jenkins.BIT-ERP
            userProfile =  КаталогЮзера + "jenkins.BIT-ERP";
            ПапкаAPPDATA = СтрШаблон("%1\AppData\Roaming", userProfile);
            ОбновитьБазуВСпискеДЛяПользователя(ПапкаAPPDATA + "\1C\1CEStart\ibases.v8i", userProfile);

            // Обновляем список баз пользователя jenkins
            userProfile =  КаталогЮзера + "jenkins";
            ПапкаAPPDATA = СтрШаблон("%1\AppData\Roaming", userProfile);
            ОбновитьБазуВСпискеДЛяПользователя(ПапкаAPPDATA + "\1C\1CEStart\ibases.v8i", userProfile);

        Исключение
            Лог.Информация("Ошибка " + КаталогЮзера + ". Детальное представление - " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
        КонецПопытки;
            
    КонецЦикла;

КонецПроцедуры

Процедура ОбновитьБазуВСпискеДЛяПользователя(ПутьКФайлуIBases, userProfile)

    Лог.Информация("Updating ibases.v8i for user %1 and base %2... ", userProfile, БАЗА);

    Попытка 
        Файл = Новый Файл(ПутьКФайлуIBases);
        Если Не Файл.Существует() Тогда
            Лог.Информация("File ibases.v8i not found. Attempting to write empty file...");
            ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлуIBases);
            ЗаписьТекста.Записать("");
            ЗаписьТекста.Закрыть();
            Лог.Информация("File ibases.v8i - ""%1"" was written sucessfully.", ПутьКФайлуIBases);
        КонецЕсли;

        ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлуIBases);
        СтрокаСпискаБаз =  ЧтениеТекста.Прочитать();
        БазаВСписке = СтрШаблон("Connect=Srvr=""%2"";Ref=""%1"";", БАЗА, СЕРВЕР);
        ЕстьБазаВСписке = СтрНайти(СтрокаСпискаБаз, БазаВСписке) <> 0;
        ЧтениеТекста.Закрыть();
        Если (ЕстьБазаВСписке) Тогда
            Лог.Информация("Infobase %1 already exists in ibases.v8i %2.", БазаВСписке, ПутьКФайлуIBases);
            Возврат;
        КонецЕсли;

        id = Строка(Новый УникальныйИдентификатор);
        ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлуIBases, КодировкаТекста.UTF8,, Истина);
        ЗаписьТекста.ЗаписатьСтроку(СтрШаблон(ШаблонНовойБазыВСписке(), БАЗА, СЕРВЕР, id));
        ЗаписьТекста.Закрыть();
        Лог.Информация("Ibases.v8i for user %1 and base %2 updated", userProfile, БАЗА);
    Исключение
        Лог.Информация(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
    КонецПопытки

КонецПроцедуры

Функция ШаблонНовойБазыВСписке()
    Возврат 
        "
        |[%1]
        |Connect=Srvr=""%2"";Ref=""%1"";
        |ID=%3
        |OrderInList=16512
        |Folder=/
        |OrderInTree=98560
        |External=0
        |ClientConnectionSpeed=Normal
        |App=Auto
        |WA=1
        |Version=8.3
        |AdditionalParameters=/TESTMANAGER";
КонецФункции

Инициализация();
Лог.Информация("Adding base to the ibases.v8i...");
ДобавитьБазуВСписок();
Лог.Информация("script completed");