#Использовать v8runner
#Использовать cmdline

Перем СЕРВЕР;
Перем СЕРВЕР_ПОРТ;
Перем БАЗА;
Перем ЭТО_ФАЙЛОВАЯ_БАЗА;
Перем ПОЛЬЗОВАТЕЛЬ;
Перем ПАРОЛЬ;
Перем ПЛАТФОРМА_ВЕРСИЯ;
Перем ИМЯ_РАСШИРЕНИЯ;
Перем КАТАЛОГ_ХРАНИЛИЩА;
Перем ЮЗЕР_ХРАНИЛИЩА;
Перем ПАРОЛЬ_ХРАНИЛИЩА;
Перем НОВЫЙ_ЮЗЕР_ХРАНИЛИЩА;
Перем НОВЫЙ_ПАРОЛЬ_ХРАНИЛИЩА;

Перем Конфигуратор;
Перем Лог;

Функция Инициализация()

    Парсер = Новый ПарсерАргументовКоманднойСтроки();
    Парсер.ДобавитьИменованныйПараметр("-platform");
    Парсер.ДобавитьИменованныйПараметр("-server");
    Парсер.ДобавитьИменованныйПараметр("-base");
    Парсер.ДобавитьИменованныйПараметр("-user");
    Парсер.ДобавитьИменованныйПараметр("-passw");
    Парсер.ДобавитьИменованныйПараметр("-extension");
    Парсер.ДобавитьИменованныйПараметр("-repouser");
    Парсер.ДобавитьИменованныйПараметр("-repopwd");
    Парсер.ДобавитьИменованныйПараметр("-newuser");
    Парсер.ДобавитьИменованныйПараметр("-repofolder");
    Парсер.ДобавитьИменованныйПараметр("-newpwd");

    Параметры = Парсер.Разобрать(АргументыКоманднойСтроки);
    
    ПЛАТФОРМА_ВЕРСИЯ  = Параметры["-platform"];//"8.3.10.2639"; // если пустая строка, то будет взята последняя версия
    СЕРВЕР            =  Параметры["-server"];
    СЕРВЕР_ПОРТ       = 1541; // 1541 - по умолчанию
    БАЗА              = Параметры["-base"];
    ЭТО_ФАЙЛОВАЯ_БАЗА = Не ЗначениеЗаполнено(СЕРВЕР);
    ПОЛЬЗОВАТЕЛЬ      = Параметры["-user"];
    ПАРОЛЬ            = Параметры["-passw"];
    ИМЯ_РАСШИРЕНИЯ    = Параметры["-extension"];
    КАТАЛОГ_ХРАНИЛИЩА = Параметры["-repofolder"];
    ЮЗЕР_ХРАНИЛИЩА    = Параметры["-repouser"];
    ПАРОЛЬ_ХРАНИЛИЩА  = Параметры["-repopwd"];
    НОВЫЙ_ЮЗЕР_ХРАНИЛИЩА = Параметры["-newuser"];
    НОВЫЙ_ПАРОЛЬ_ХРАНИЛИЩА = Параметры["-newpwd"];
    
    //ПЛАТФОРМА_ВЕРСИЯ  = "8.3.12.1685";
    //СЕРВЕР            = "devadapter";
    //СЕРВЕР_ПОРТ       = 1541; // 1541 - по умолчанию
    //БАЗА              = "rkudakov_adapter_adapter";
    //ЭТО_ФАЙЛОВАЯ_БАЗА = Не ЗначениеЗаполнено(СЕРВЕР);
    //ПОЛЬЗОВАТЕЛЬ      = "Administrator";
    //ПАРОЛЬ            = "111";
    //ИМЯ_РАСШИРЕНИЯ    = "ext1";
    //КАТАЛОГ_ХРАНИЛИЩА = "tcp://storage1c.bit-erp.loc:2542/testrepo";
    //ЮЗЕР_ХРАНИЛИЩА = "auto";
    //ПАРОЛЬ_ХРАНИЛИЩА = "111";
    //НОВЫЙ_ЮЗЕР_ХРАНИЛИЩА = "rkudakov";
    //НОВЫЙ_ПАРОЛЬ_ХРАНИЛИЩА = "111";

    Конфигуратор = Новый УправлениеКонфигуратором();
    Конфигуратор.УстановитьКонтекст(СтрокаСоединенияИБ(), ПОЛЬЗОВАТЕЛЬ, ПАРОЛЬ);
    Конфигуратор.ИспользоватьВерсиюПлатформы(ПЛАТФОРМА_ВЕРСИЯ);

    Лог = Логирование.ПолучитьЛог("loadExtension");
    ЛОГ.Отладка("СЕРВЕР = " + СЕРВЕР);
    ЛОГ.Отладка("ПЛАТФОРМА_ВЕРСИЯ = " + ПЛАТФОРМА_ВЕРСИЯ);

КонецФункции

Процедура ДобавитьПользователяВРасширение()
    
    Параметры = Конфигуратор.ПолучитьПараметрыЗапуска();

    Параметры.Добавить("/ConfigurationRepositoryF """ + КАТАЛОГ_ХРАНИЛИЩА + """");
    Параметры.Добавить("/ConfigurationRepositoryN """ + ЮЗЕР_ХРАНИЛИЩА + """");
    Параметры.Добавить("/ConfigurationRepositoryP """ + ПАРОЛЬ_ХРАНИЛИЩА + """");
    Параметры.Добавить("/ConfigurationRepositoryAddUser ");
    Параметры.Добавить(СтрШаблон("-User ""%1""", НОВЫЙ_ЮЗЕР_ХРАНИЛИЩА));
    Параметры.Добавить(СтрШаблон("-Pwd ""%1""", НОВЫЙ_ПАРОЛЬ_ХРАНИЛИЩА));
    Параметры.Добавить(СтрШаблон("-Rights %1", "LockObjects"));
    Параметры.Добавить("-Extension " + ИМЯ_РАСШИРЕНИЯ);

    Конфигуратор.ВыполнитьКоманду(Параметры);

КонецПроцедуры 

Функция СтрокаСоединенияИБ() 
    Если ЭТО_ФАЙЛОВАЯ_БАЗА Тогда
        Возврат "/F" + БАЗА; 
    Иначе   
        Возврат "/IBConnectionString""Srvr=" + СЕРВЕР + ?(ЗначениеЗаполнено(СЕРВЕР_ПОРТ),":" + СЕРВЕР_ПОРТ,"") + ";Ref='"+ БАЗА + "'""";
    КонецЕсли;
КонецФункции

Инициализация();
Лог.Информация("Adding user %1 for extension storage %2", НОВЫЙ_ЮЗЕР_ХРАНИЛИЩА, КАТАЛОГ_ХРАНИЛИЩА);
ДобавитьПользователяВРасширение();
Лог.Информация("Sucessfuly added user %1", НОВЫЙ_ЮЗЕР_ХРАНИЛИЩА);